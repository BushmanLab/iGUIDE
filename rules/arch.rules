# -*- mode: Snakemake -*-
# Architecture Rules
# Related to setting up analysis directories and consolidating data

rule setup_working_dir:
  input: "configs/" + RUN + ".config.yml"
  output: directory("analysis/" + RUN)
  shell:
    """
    WORK_DIR="{ROOT_DIR}/analysis/{RUN}"
    mkdir ${{WORK_DIR}}
    cd ${{WORK_DIR}}
    ln -s ../../{input} config.yml
    mkdir input_data
    mkdir logs
    mkdir process_data
    mkdir output
    mkdir reports
    cd ../

    echo "'{RUN}' setup has completed."
    """

rule generate_stat_matrix:
  input:
    demulti=RUN_DIR + "/process_data/" + RUN + ".demulti.stat",
    trimR1=expand(
      RUN_DIR + "/process_data/{sample}.R1.trim.stat", sample=SAMPLES),
    trimPrimer=expand(
      RUN_DIR + "/process_data/{sample}.R2.primer.trim.stat", sample=SAMPLES),
    trimODN=expand(
      RUN_DIR + "/process_data/{sample}.R2.trim.stat", sample=SAMPLES),
    umitags=expand(
      RUN_DIR + "/process_data/{sample}.umitags.stat", sample=SAMPLES),
    filt=expand(
      RUN_DIR + "/process_data/{sample}.filt.stat", sample=SAMPLES),
    consol=expand(
      RUN_DIR + "/process_data/{sample}.{read}.consol.stat", 
      sample=SAMPLES, read=READS),
    align=expand(
      RUN_DIR + "/process_data/{sample}.align.stat", sample=SAMPLES),
    assim=RUN_DIR + "/process_data/" + RUN + ".assim.stat"
  output:
    RUN_DIR + "/output/stats." + RUN + ".csv"
  params:
    dir=RUN_DIR + "/process_data",
    tool=ROOT_DIR + "/tools/rscripts/collect_stats.R"
  shell:
    """
    Rscript {params.tool} {params.dir} -o {output}
    """
