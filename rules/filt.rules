# -*- mode: Snakemake -*-
# Sequence Filtering Rules

rule seq_filter:
  input:
    R1=RUN_DIR + "/process_data/trimmed/{sample}.R1.trim.fastq.gz",
    R2=RUN_DIR + "/process_data/trimmed/{sample}.R2.trim.fastq.gz"
  output:
    R1=temp(RUN_DIR + "/process_data/filtered/{sample}.R1.filt.fastq.gz"),
    R2=temp(RUN_DIR + "/process_data/filtered/{sample}.R2.filt.fastq.gz"),
    stat=temp(RUN_DIR + "/process_data/stats/{sample}.filt.stat")
  params:
    tool=ROOT_DIR + "/tools/rscripts/filt.R",
    readNamePatternArg=config["readNamePattern"]
  log:
    RUN_DIR + "/logs/{sample}.filt.log"
  resources:
    mem_mb=lambda wildcards, attempt: attempt * config["filtMB"]
  shell:
    """
    Rscript {params.tool} {input.R1} {input.R2} -o {output.R1} {output.R2} \
      --readNamePattern {params.readNamePatternArg} \
      --stat {output.stat} --compress > {log} 2>&1
    """

rule bin_passing_reads:
  input:
    expand(
      RUN_DIR + "/process_data/filtered/{{sample}}.{read}.filt.fastq.gz",
      read=READS
    )
  output:
    temp(expand(
      RUN_DIR + "/process_data/binned/{{sample}}.{read}.filt.{bin}.fastq.gz", 
      read=READS, bin=BINS
    ))
  params:
    tool=ROOT_DIR + "/tools/rscripts/bin_seqs.R",
    outdir=RUN_DIR + "/process_data/binned",
    bins=config["bins"],
    level=config["level"],
    readNamePatternArg=config["readNamePattern"]
  log:
    RUN_DIR + "/logs/{sample}.bin.log"
  resources:
    mem_mb=lambda wildcards, attempt: attempt * config["defaultMB"]
  shell:
    """
    Rscript {params.tool} {input} -o {params.outdir} \
      -b {params.bins} -l {params.level} --compress \
      --readNamePattern {params.readNamePatternArg} > {log} 2>&1
    """

