# -*- mode: Snakemake -*-
# Sequence Alignment: BLAT

rule align:
  input:
    seq = RUN_DIR + "/processData/{sample}.{read}.consol.fasta",
    genome = ancient(ROOT_DIR + "/" + config["Ref_Genome_Path"])
  output:
    temp(RUN_DIR + "/processData/{sample}.{read}.psl")
  params: 
    config["BLATparams"]
  log:
    RUN_DIR + "/logs/{sample}.{read}.blat.log"
  resources:
    mem_mb = lambda wildcards, attempt: attempt * config["alignMB"]
  shell:
    """
    if [[ $(cat {input.seq} | wc -l) -eq 0 ]]
        then
            touch {output}
            echo 'Empty input sequences for {input.seq}.' > {log} 2>&1
        else
            blat {input.genome} {input.seq} {output} \
                {params} > {log} 2>&1
    fi
    """

rule compress_align:
  input: RUN_DIR + "/processData/{sample}.{type}.psl"
  output: temp(RUN_DIR + "/processData/{sample}.{type}.psl.gz")
  shell: "gzip {input}"

rule generate_2bit:
  input: ancient("configs/" + RUN + ".config.yml")
  output: ROOT_DIR + "/" + config["Ref_Genome_Path"]
  params: config["Ref_Genome"]
  shell: 
    "Rscript {ROOT_DIR}/tools/rscripts/generate_2bit_genome.R {params} {output}"

