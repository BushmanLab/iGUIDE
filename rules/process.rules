# -*- mode: Snakemake -*-
# Final Processing Rules

rule all_uniq_sites:
  input:
    expand(RUN_DIR + "/processData/{sample}.uniq.csv", sample=SAMPLES)
  output:
    RUN_DIR + "/output/unique_sites." + RUN + ".csv"
  params:
    RUN_DIR + "/processData"
  shell:
    """
    head -n 1 -q {params}/*.uniq.csv | uniq > {output}
    cat {params}/*.uniq.csv | sed '/seqnames/d' >> {output}
    """

def all_umitag_inputs(wildcards):
  if (config["UMItags"]):
    return expand(
      RUN_DIR + "/processData/{sample}.umitags.fasta.gz", sample = SAMPLES)
  else:
    return []

def all_multi_inputs(wildcards):
  if (config["recoverMultihits"]):
    return expand(
      RUN_DIR + "/processData/{sample}.multihits.rds", sample = SAMPLES)
  else:
    return []

rule all_edit_sites:
  input:
    sites=RUN_DIR + "/output/unique_sites." + RUN + ".csv",
    umitag=all_umitag_inputs,
    multi=all_multi_inputs
  output:
    RUN_DIR + "/output/edited_sites." + RUN + ".rds"
  params:
    config = RUN_DIR + "/" + "config.yml",
    tool = ROOT_DIR + "/tools/processing/post_process.R"
  log:
    RUN_DIR + "/logs/" + RUN + ".postproc.log"
  run:
    call_str="Rscript {params.tool} {input.sites} -o {output} "
    if (config["UMItags"]):
        call_str=call_str + "-u {input.umitag} "
    if (config["recoverMultihits"]):
        call_str=call_str + "-m {input.multi} "
    call_str=call_str + "-c {params.config} > {log} 2>&1"
    shell(call_str)

def report_supp(wildcards):
  supp_str = str()
  if (config["figures"]): 
      supp_str = supp_str + "-f "
  if (config["reportData"]):
      supp_str = supp_str + "-d "
  if (config["suppFile"]):
      supp_str = supp_str + "-s " + ROOT_DIR + "/" + config["Supplemental_Info"]
  return supp_str
        
rule run_report:
  input: RUN_DIR + "/output/edited_sites." + RUN + ".rds"
  output: RUN_DIR + "/reports/report." + RUN + ".html"
  params: 
    tool = ROOT_DIR + "/tools/processing/generate_iGUIDE_report.R",
    config = RUN_DIR + "/" + "config.yml",
    supp = report_supp
  log: RUN_DIR + "/logs/" + RUN + ".report.log"
  shell:
    """
    Rscript {params.tool} {input} -o {output} \
      -c {params.config} {params.supp} > {log} 2>&1
    """
